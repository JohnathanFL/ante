effect Emit a with
    emit: a -> Unit

iota (n: U32) = loop (i = 0) ->
    if i < n then
        emit i
        recur (i + 1)

filter (stream: Unit -> Unit can Emit a) (f: a -> Bool pure) =
    handle stream ()
    | emit x ->
        if f x then
            emit x
        resume ()

map (stream: Unit -> Unit can Emit a) (f: a -> b pure): Unit can Emit b =
    handle stream ()
    | emit x ->
        emit (f x)
        resume ()

for (stream: Unit -> Unit can Emit a) (f: a -> Unit pure) =
    handle stream ()
    | emit x ->
        f x
        resume ()

even x = x % 2 == 0

iota 10
    with filter even
    with map (_ * 2)
    with for print

// args: --delete-binary
// expected stdout:
// 0
// 4
// 8
// 12
// 16
